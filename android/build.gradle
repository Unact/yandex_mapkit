def variant = project.hasProperty("yandexMapkit.variant") ? project.property("yandexMapkit.variant") : "lite"

group 'com.unact.yandexmapkit'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.unact.yandexmapkit'
    }

    sourceSets {
        main {
            java {
                include("com/unact/yandexmapkit/lite/*")

                if (variant == "lite") {
                    exclude("com/unact/yandexmapkit/full/*")
                }
            }
        }
    }

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21

        buildConfigField "String", "YANDEX_MAPKIT", "\"${variant}\""
    }
}

dependencies {
    implementation 'com.yandex.android:maps.mobile:4.5.1-' + variant
}
